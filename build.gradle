import org.jetbrains.grammarkit.tasks.*

plugins {
    id 'org.jetbrains.intellij' version "0.7.3"
    id "org.jetbrains.grammarkit" version "2021.1"
}

sourceSets {
    main {
        java {
            srcDirs += ["src/main/gen"]
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

sourceCompatibility = 1.8
targetCompatibility = 1.8

version '1.1.4'
group 'com.jantvrdik.intellij.latte'

jar {
    archiveName 'intellij-latte-' + version + '.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

runIde {
    ideDirectory "${properties.getProperty('runIdeDirectory')}"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

patchPluginXml {
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

apply plugin: 'org.jetbrains.intellij'

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            'java',
            'properties'
    ]
    pluginName 'Latte'
}

repositories {
    mavenCentral()
    maven { url = 'https://mvnrepository.com/artifact/org.hamcrest/hamcrest-core' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compileOnly 'com.intellij.psi:java-psi-api-142.1'
    compileOnly 'com.intellij.psi:css-openapi'
    compileOnly 'com.intellij.psi:javascript-openapi'
    compileOnly 'org.hamcrest:hamcrest-core:1.3'
    compileOnly 'com.mesour.intellij:intellij-utils:1.0.0-SNAPSHOT'
    testCompile 'junit:junit:4.13'
}

apply plugin: 'org.jetbrains.grammarkit'

grammarKit {
    jflexRelease = '1.7.0-1'
    grammarKitRelease = '6452fde524'
}

buildSearchableOptions {
    enabled = false // this will fix java.lang.Throwable caused by buildSearchableOptions - https://youtrack.jetbrains.com/issue/KTIJ-782
}

/*task generateLatteParser(type: GenerateParser) {
    source = "src/main/java/com/jantvrdik/intellij/latte/parser/LatteParser.bnf"
    targetRoot = "src/main/gen"
    pathToParser = '/com/jantvrdik/intellij/latte/parser/LatteParser.java'
    pathToPsiRoot = '/com/jantvrdik/intellij/latte/psi'
    purgeOldFiles = true
}*/

task generateLatteLexer(type: GenerateLexer) {
    source = "src/main/java/com/jantvrdik/intellij/latte/parser/_LatteLexer.flex"
    targetDir = "src/main/gen/com/jantvrdik/intellij/latte/parser/"
    targetClass = "_LatteLexer"
    purgeOldFiles = true
}

task generateLatteMacroContentLexer(type: GenerateLexer) {
    source = "src/main/java/com/jantvrdik/intellij/latte/lexer/grammars/LatteMacroContentLexer.flex"
    targetDir = "src/main/gen/com/jantvrdik/intellij/latte/lexer/"
    targetClass = "LatteMacroContentLexer"
    purgeOldFiles = true
}

task generateLatteMacroLexer(type: GenerateLexer) {
    source = "src/main/java/com/jantvrdik/intellij/latte/lexer/grammars/LatteMacroLexer.flex"
    targetDir = "src/main/gen/com/jantvrdik/intellij/latte/lexer/"
    targetClass = "LatteMacroLexer"
    purgeOldFiles = true
}

task generateLatteTopLexer(type: GenerateLexer) {
    source = "src/main/java/com/jantvrdik/intellij/latte/lexer/grammars/LatteTopLexer.flex"
    targetDir = "src/main/gen/com/jantvrdik/intellij/latte/lexer/"
    targetClass = "LatteTopLexer"
    purgeOldFiles = true
}

task generateLattePhpLexer(type: GenerateLexer) {
    source = "src/main/java/com/jantvrdik/intellij/latte/lexer/grammars/LattePhpLexer.flex"
    targetDir = "src/main/gen/com/jantvrdik/intellij/latte/lexer/"
    targetClass = "LattePhpLexer"
    purgeOldFiles = true
}

task generateFlexFiles {
    println('Generating Classes (Lexer)')
    //dependsOn(generateLatteParser)
    dependsOn(generateLatteLexer)
    dependsOn(generateLatteMacroContentLexer)
    dependsOn(generateLatteMacroLexer)
    dependsOn(generateLattePhpLexer)
    dependsOn(generateLatteTopLexer)
}

compileJava.dependsOn(generateFlexFiles)
